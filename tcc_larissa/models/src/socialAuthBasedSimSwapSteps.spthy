functions: permission/2 [private], question/2, answers/3[private], correctAnswer/1 [private], 
           sendConfirmationToSecureContact/3, getSentPin/1

equations:
    answers(T, K, correctAnswer(question(T, K))) = permission(T, K),
    getSentPin(sendConfirmationToSecureContact(T, K, P)) = P


rule InitSimSwapWithMNORepresentative:
    [
      !MNORepresentative(O, repId),  MaskPerform(U, userId, phoneNumber, action), Fr(~seedK)
    ]
    --[Eq(action, 'CALL_CUSTOMER_SERVICE_REPRESENTATIVE'), InitSimSwap(U, phoneNumber, ~seedK)]->
    [
        Status('INIT', repId, userId, phoneNumber, ~seedK),
        MaskReactant(U, userId, phoneNumber, question(phoneNumber, ~seedK)),
        Out(question(phoneNumber, ~seedK)),
        Out(~seedK)
    ]  


rule AnswerSimSwapToMNO:
  
    [   
        !MNOAccount(U, userId, userName, initialUserPhoneNumber, userSecureContact),
        MaskPerform(O, repIdH, repIdH, <action, data>),
        Fr(~newICCID), Fr(~pinICCID),
        In(seedK)
    ]
    --[Eq(action, 'REPRESENTATIVE_CONFIRMS'), Eq(initialUserPhoneNumber, data), AnswerSimSwap(initialUserPhoneNumber, seedK)]->
    [
        Status('AUTH_COMPLETED', repIdH, userId, initialUserPhoneNumber, <seedK, ~pinICCID, ~newICCID>),
        UserCreatedRequest(userId, initialUserPhoneNumber, <seedK, ~pinICCID, ~newICCID>)
    ]

 
rule MNOsendsMessageToRandomSecureContact:
    let
      userSecureContactMessage = sendConfirmationToSecureContact(phoneNumber, ~random, pinICCID)
    in
    [ 
        Status('AUTH_COMPLETED', repId, userId, phoneNumber, <seedK, pinICCID, newICCID>), 
        Fr(~random)
    ]
    --[ SendMessageSimSwap(phoneNumber, ~random) ]->
    [ 
      Out_S($N,$S,userSecureContactMessage),
      Status('CONFIRMATION_SENT', repId, userId, phoneNumber, <seedK, pinICCID, newICCID>)
    ]

rule SecureContactComparesPinAndFinishSimSwap:
    [
        MaskPerform($N, ~secureId, userSecureContacts, <action, pinICCID>),  
        Status('CONFIRMATION_SENT', repId, userId, phoneNumber, <seedK, pinICCID, newICCID>)
    ]
    --[ Eq(action, 'confirms_pin'), ConfirmsPinSimSwap(phoneNumber, seedK)]->
    [
        FinishSwap(phoneNumber, pinICCID, seedK, newICCID)
    ]

rule FinishSimSwapOnNetwork:
    [
        FinishSwap(phoneNumber, pinICCID, seedK, newICCID)
    ]
    --[ FinishSimSwap(phoneNumber, seedK) ]->
    [ 
        !UserICCID(newICCID, phoneNumber)
    ]
